-- This file is part of the yasmv distribution
-- (c) 2011-2016 M. Pensallorto < marco DOT pensallorto AT gmail DOT com >
MODULE ferryman

@inertial
VAR
        ferryman : { EAST, WEST };
        goat     : { EAST, WEST };
        cabbage  : { EAST, WEST };
        wolf     : { EAST, WEST };
        carry    : { NIL, GOAT, CABBAGE, WOLF };

-- Everything is on the WEST bank at the beginning
INIT
        ferryman = WEST &&
        goat     = WEST &&
        cabbage  = WEST &&
        wolf     = WEST &&
        carry    = NIL   ;

-- If the FERRYMAN has left the CABBAGE behind, the GOAT is not with the CABBAGE.
INVAR
        ferryman != cabbage -> goat != cabbage;

-- If the FERRYMAN has left the GOAT behind, the WOLF is not with the GOAT.
INVAR
        ferryman != goat -> wolf != goat;

-- A mandatory non-deterministic choice. Notice that this TRANS is unguarded.
TRANS
        carry := {(ferryman = goat    ? GOAT    : NIL),
                  (ferryman = cabbage ? CABBAGE : NIL),
                  (ferryman = wolf    ? WOLF    : NIL)};

-- If the FERRYMAN is carrying the GOAT, FERRYMAN and GOAT will have the same location.
TRANS
        next(carry) = GOAT ?:
                      goat := next(ferryman);

-- If the FERRYMAN is carrying the CABBAGE, FERRYMAN and CABBAGE will have the same location.
TRANS
       next(carry) = CABBAGE ?:
                     cabbage := next(ferryman);

-- If the FERRYMAN is carrying the WOLF, FERRYMAN and WOLF will have the same location.
TRANS
     next(carry) = WOLF ?:
                   wolf := next(ferryman);

-- If the FERRYMAN is RUNNING, she has to switch sides at each step.
TRANS
     ferryman :=
              (ferryman = WEST ? EAST : WEST);

-- To perform planning, we need to assert the negation of the desired TARGET.
-- Our TARGET here is to have all the required items on the EAST bank.

-- Symbols annotated with `@hidden` do not show up in traces.
@hidden
DEFINE GOAL :=
       cabbage = EAST && goat = EAST && wolf = EAST;

-- To solve the ferryman problem in this model try this:
-- >> reach GOAL; dump-trace

