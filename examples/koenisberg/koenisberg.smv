-- This file is part of the YASMINE distribution
-- (c) 2011-2016 M. Pensallorto < marco DOT pensallorto AT gmail DOT com >
#word-width 3
MODULE koenisberg

@inertial
VAR
  location: { A, B, C, D } ;
  marked  : boolean[7];
  choice  : uint;

INIT
  marked = [ FALSE, FALSE, FALSE, FALSE, FALSE, FALSE, FALSE ];

DEFINE
  GOAL :=
       marked = [ TRUE, TRUE, TRUE, TRUE, TRUE, TRUE, TRUE ];

INVAR
  ! GOAL -> choice <= 6;

INVAR
  location = A -> next(location) = { B, C, D };

INVAR
  location = B -> next(location) = { B, C };

INVAR
  location = C -> next(location) = { A, B, D };

INVAR
  location = D -> next(location) = { A, C };

TRANS
  location = A && next(location) = B ||
  location = B && next(location) = A ?:
           choice := { 2, 3 };

TRANS
  location = A && next(location) = D ||
  location = D && next(location) = A ?:
           choice := { 0, 1 };

TRANS
  location = A && next(location) = C ||
  location = C && next(location) = A ?:
           choice := 4;

TRANS
  location = B && next(location) = C ||
  location = C && next(location) = B ?:
           choice := 5 ;

TRANS
  location = C && next(location) = D || 
  location = D && next(location) = C ?:
           choice := 6;

INVAR
  ! marked [choice];

TRANS
  marked := [(choice = 0 ? TRUE : marked[0]),
             (choice = 1 ? TRUE : marked[1]),
             (choice = 2 ? TRUE : marked[2]),
             (choice = 3 ? TRUE : marked[3]),
             (choice = 4 ? TRUE : marked[4]),
             (choice = 5 ? TRUE : marked[5]),
             (choice = 6 ? TRUE : marked[6])];
